plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.12'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'com.mzc'
version = '0.0.1-SNAPSHOT'
description = 'MZC Learning Platform Backend API'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Thumbnail generation
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'

    // Excel parsing (Apache POI)
    implementation 'org.apache.poi:poi:5.2.5'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'

    // PDF generation (OpenPDF - LGPL license, fork of iText)
    implementation 'com.github.librepdf:openpdf:2.0.3'

    // CSV BOM handling
    implementation 'commons-io:commons-io:2.15.1'

    // Character encoding detection
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'

    // OpenAPI / Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    environment 'JWT_SECRET', 'mzc-lp-test-jwt-secret-key-that-is-at-least-256-bits-long-for-hmac-sha256'
    finalizedBy jacocoTestReport
}

// JaCoCo 설정
jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

// SonarQube 설정
sonar {
    properties {
        property "sonar.projectKey", "mzc-lp-backend"
        property "sonar.projectName", "MZC LP Backend"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.token", System.getenv("SONAR_TOKEN") ?: ""
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.sourceEncoding", "UTF-8"
    }
}
